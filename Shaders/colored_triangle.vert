#version 450
#extension GL_EXT_buffer_reference : require // for buffer_reference

layout (location = 0) out vec3 outColor;
layout (location = 1) out vec2 outUV;

struct Vertex {
	vec3 position;
	float uv_x;
	vec3 normal;
	float uv_y;
	vec4 color;
};

layout(buffer_reference, std430) readonly buffer VertexBuffer {
	Vertex vertices[/*unbound*/];
};

layout (push_constant) uniform constants 
{
	mat4 worldMatrix;
	VertexBuffer vertexBuffer; // uint64 handle due to buffer_reference
} PushConstants;

void main()
{
	Vertex v = PushConstants.vertexBuffer.vertices[gl_VertexIndex];
	
	gl_Position = PushConstants.worldMatrix * vec4(v.position, 1.0f);
	outColor = v.color.xyz;
	outUV.x = v.uv_x;
	outUV.y = v.uv_y;
}